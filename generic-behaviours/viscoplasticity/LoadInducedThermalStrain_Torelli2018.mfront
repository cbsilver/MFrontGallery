@Parser Implicit;
@Behaviour LoadInducedThermalStrain_Torelli2018;
@Author Giacomo Torelli;
@Date 25 / 03 / 2018;
@Description {
  "A confinement-dependent load-induced thermal strain "
  "constitutive model for concrete subjected "
  "to temperatures up to 500 Â°C"

  "Torelli, Giacomo and Mandal, Parthasarathi and Gillie, "
  "Martin and Tran, Van-Xuan"

  "International Journal of Mechanical Sciences"

  "http://www.sciencedirect.com/science/article/pii/S0020740317337372"
}

@Algorithm NewtonRaphson_NumericalJacobian;
@Theta 0.5;
@IterMax 100;
@Epsilon 1.E-14;
@PerturbationValueForNumericalJacobianComputation 1.e-8;

@ModellingHypotheses{".+"};
@Brick StandardElasticity;

@ElasticMaterialProperties{47000, 0.25};

@Parameter alpha = 10e-6;
@Parameter gamma = 1.5;
@Parameter sigmultimate = 50.;
@Parameter nulits = 0.50;
@Parameter tcrit = 0.;
@Parameter b[5] = {2.7031065533E-05, -1.0209170592E-06, 6.1200423753E-9, -1.2632648735E-11,
                   6.9158539621E-15};

@StateVariable Stensor ELITS;
@AuxiliaryStateVariable Stensor EFTS;
@AuxiliaryStateVariable real Tmax;

// ! thermal strain increment (scalar)
@LocalVariable StrainStensor deth;

@InitLocalVariables {
  Tmax = max(max(tcrit, Tmax), T);
  deth = alpha * dT * Stensor::Id();
}

@Integrator {
  const auto T_ = T + dT;
  if ((T_ > Tmax) && (trace(sig) < 0)) {
    // Decomposition of the stress tensor in positive and negative
    // parts
    const auto sig_p = positive_part(sig);
    const auto sig_n = sig - sig_p;
    //----- Multiaxial correction coefficient
    const auto s_den = sqrt(sig_n | sig_n);
    real cm = 1;
    if (s_den > 1.e-12 * young) {
      cm = -trace(sig_n) / s_den;
    }
    const auto eta = 1 + (cm - 1) * gamma;
    const auto mybeta = b[0] + T * (b[1] + T * (b[2] + T * (b[3] + T * b[4])));
    fELITS -= (mybeta / (-sigmultimate)) * eta *
              ((1 + nulits) * (sig_n)-nulits * trace(sig_n) * Stensor::Id()) * (T + dT - Tmax);
  }
  // elasticity
  feel += dELITS + deth;
}

@UpdateAuxiliaryStateVariables {
  EFTS += deth;
}